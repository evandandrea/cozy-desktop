port module Settings exposing (..)

import Html exposing (..)
import Html.Attributes exposing (..)
import Html.Events exposing (..)
import Helpers exposing (Helpers)


-- MODEL


type alias DiskSpace =
    { used : Float
    , quota : Float
    }


type alias Model =
    { version : String
    , newRelease : Maybe ( String, String )
    , autoLaunch : Bool
    , address : String
    , deviceName : String
    , disk : DiskSpace
    , busyUnlinking : Bool
    }


init : String -> Model
init version =
    { version = version
    , newRelease = Nothing
    , autoLaunch = True
    , address = ""
    , deviceName = ""
    , disk =
        { used = 0
        , quota = 0
        }
    , busyUnlinking = False
    }



-- UPDATE


type Msg
    = SetAutoLaunch Bool
    | AutoLaunchSet Bool
    | QuitAndInstall
    | NewRelease ( String, String )
    | FillAddressAndDevice ( String, String )
    | UpdateDiskSpace DiskSpace
    | UnlinkCozy
    | CancelUnlink


port unlinkCozy : () -> Cmd msg


port autoLauncher : Bool -> Cmd msg


port quitAndInstall : () -> Cmd msg


update : Msg -> Model -> ( Model, Cmd Msg )
update msg model =
    case
        msg
    of
        SetAutoLaunch autoLaunch ->
            ( model, autoLauncher autoLaunch )

        AutoLaunchSet autoLaunch ->
            ( { model | autoLaunch = autoLaunch }, Cmd.none )

        QuitAndInstall ->
            ( model, quitAndInstall () )

        NewRelease ( notes, name ) ->
            ( { model | newRelease = Just ( notes, name ) }, Cmd.none )

        FillAddressAndDevice ( address, deviceName ) ->
            ( { model | address = address, deviceName = deviceName }, Cmd.none )

        UpdateDiskSpace disk ->
            ( { model | disk = disk }, Cmd.none )

        UnlinkCozy ->
            ( { model | busyUnlinking = True }, unlinkCozy () )

        CancelUnlink ->
            ( { model | busyUnlinking = False }, Cmd.none )



-- VIEW


humanReadableDiskValue : Helpers -> Float -> String
humanReadableDiskValue helpers v =
    (toString (round (v / 1000000)) ++ " M" ++ (helpers.t "Account b"))


diskQuotaLine : Helpers -> Model -> Html Msg
diskQuotaLine helpers model =
    if model.disk.quota == 0 then
        div []
            [ text ((humanReadableDiskValue helpers model.disk.used) ++ " / âˆž") ]
    else
        div []
            [ text
                ((humanReadableDiskValue helpers model.disk.used)
                    ++ " / "
                    ++ (humanReadableDiskValue helpers model.disk.quota)
                )
            , (progressbar (model.disk.used / model.disk.quota))
            ]


versionLine : Helpers -> Model -> Html Msg
versionLine helpers model =
    case model.newRelease of
        Just ( name, changes ) ->
            span [ class "version-need-update" ]
                [ text model.version
                , a [ onClick QuitAndInstall, href "#", class "btn btn--action" ]
                    [ text (helpers.t "Settings Install the new release and restart the application") ]
                ]

        Nothing ->
            span [ class "version-uptodate" ] [ text model.version ]


progressbar : Float -> Html Msg
progressbar ratio =
    let
        cappedRatio =
            (Basics.min 1 ratio)

        percent =
            (toString (cappedRatio * 100)) ++ "%"
    in
        div [ class "progress" ]
            [ div
                [ class "progress-inner"
                , style [ ( "width", percent ) ]
                ]
                []
            ]


view : Helpers -> Model -> Html Msg
view helpers model =
    section [ class "two-panes__content two-panes__content--settings" ]
        [ h2 [] [ text (helpers.t "Account Cozy disk space") ]
        , diskQuotaLine helpers model
        , h2 [] [ text (helpers.t "Settings Startup") ]
        , div
            [ class "coz-form-toggle"
            ]
            [ text (helpers.t "Settings Start Cozy Drive on system startup")
            , span [ class "toggle" ]
                [ input
                    [ type_ "checkbox"
                    , checked model.autoLaunch
                    , id "auto-launch"
                    , class "checkbox"
                    , onCheck SetAutoLaunch
                    ]
                    []
                , label [ class "label", for "auto-launch" ] []
                ]
            ]
        , h2 [] [ text (helpers.t "Account About") ]
        , p []
            [ strong [] [ text ((helpers.t "Account Account") ++ " ") ]
            , a [ href model.address ] [ text model.address ]
            ]
        , p []
            [ strong [] [ text ((helpers.t "Account Device name") ++ " ") ]
            , text model.deviceName
            ]
        , p []
            [ strong [] [ text ((helpers.t "Settings Version") ++ " ") ]
            , versionLine helpers model
            ]
        , h2 [] [ text (helpers.t "Account Unlink Cozy") ]
        , p []
            [ text ((helpers.t "Account It will unlink your account to this computer.") ++ " ")
            , text ((helpers.t "Account Your files won't be deleted.") ++ " ")
            , text ((helpers.t "Account Are you sure to unlink this account?") ++ " ")
            ]
        , a
            [ class "btn btn--danger"
            , href "#"
            , if model.busyUnlinking then
                attribute "aria-busy" "true"
              else
                onClick UnlinkCozy
            ]
            [ text (helpers.t "Account Unlink this Cozy") ]
        ]
